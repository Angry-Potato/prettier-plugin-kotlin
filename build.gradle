group 'com.edutron'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.3.31'
    ext.pkg_dir = 'pkg'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'kotlin2js'

sourceSets {
    main.kotlin.srcDirs += "src/main/kotato"
    test.kotlin.srcDirs += "src/test/kotato"
}

compileKotlin2Js {
    kotlinOptions.outputFile = "${projectDir}/${pkg_dir}/lib/kotlin-js-example_main.js"
    kotlinOptions.moduleKind = "umd"
}

compileTestKotlin2Js {
    kotlinOptions.moduleKind = "umd"
}

repositories {
    mavenCentral()
    maven {
        url "http://dl.bintray.com/kotlin/kotlin-eap-1.3.31"
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
    testCompile "org.jetbrains.kotlin:kotlin-test-js:$kotlin_version"
}

clean.doFirst() {
    delete("${pkg_dir}")
}

build.doLast() {
    // Copy kotlin.js and kotlin-meta.js from jar into web directory
    configurations.compile.each { File file ->
        copy {
            includeEmptyDirs = false

            from zipTree(file.absolutePath)
            into "${projectDir}/${pkg_dir}/lib"
            include { fileTreeElement ->
                def path = fileTreeElement.path
                path.endsWith(".js") && (path.startsWith("META-INF/resources/") || !path.startsWith("META-INF/"))
            }
        }
    }

    // Unpack build contents
    // TODO Remove this step when a CDN link is available.
    copy {
        File artifact = new File("build/libs/${project.name}-${project.version}.jar")
        includeEmptyDirs = false

        from zipTree(artifact)
        into "${pkg_dir}"
        include { fileTreeElement ->
            def path = fileTreeElement.path
            !(path.startsWith("META-INF/") || path.startsWith("${project.name}"))
        }
    }

    // Copy scripts to web directory
    copy {
        includeEmptyDirs = false
        from new File("build/classes/main")
        into "${pkg_dir}/lib"
    }

    copy {
        includeEmptyDirs = false
        from new File("build/classes/test")
        into "${pkg_dir}/lib"
    }

    copy {
        includeEmptyDirs = false
        from new File("build/resources/test")
        into "${pkg_dir}"
    }

}